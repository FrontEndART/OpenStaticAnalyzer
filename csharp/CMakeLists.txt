set (REVISION_TARGET Revision_csharp)
set (CSHARP_BUILD_TARGET CSHARP_solution)

string(TOLOWER ${VS_PLATFORM} CSHARP_PLATFORM)
if(${CSHARP_PLATFORM} STREQUAL win32)
    set(CSHARP_PLATFORM x86)
endif()

set (REVISION_TEMPLATE_FILE ${CMAKE_CURRENT_SOURCE_DIR}/lib/Columbus/Revision.cs.in)
set (REVISION_GENERATED_FILE ${CMAKE_CURRENT_SOURCE_DIR}/lib/Columbus/Revision.cs)

add_custom_target (
    ${REVISION_TARGET}
    DEPENDS generate_build_info
    COMMAND ${CMAKE_COMMAND} "-DINPUT_FILE_NAME=${REVISION_TEMPLATE_FILE}" "-DOUTPUT_FILE_NAME=${REVISION_GENERATED_FILE}" "-DTEMPLATE_VARIABLE_NAME=GIT_HASH" "-DTEMPLATE_VARIABLE_VALUE_FILE=${TEMP_BUILD_INFO_FILE}" -P ${CMAKE_MODULE_PATH}/ConfigureFile.cmake
    WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
)

set_target_properties (${REVISION_TARGET} PROPERTIES FOLDER ${CMAKE_SUPPORT_FOLDER_NAME})

add_custom_target (
  ${CSHARP_BUILD_TARGET}
  COMMAND dotnet build cl/CSAN/CSAN.csproj --configuration $<CONFIG> /p:Platform=${CSHARP_PLATFORM} /p:COLUMBUS_BIN_DIR=${EXECUTABLE_OUTPUT_PATH} /p:CSHARP_BUILD_DIR=${CMAKE_CURRENT_BINARY_DIR} > ${CMAKE_CURRENT_BINARY_DIR}/CSAN-build.log
  DEPENDS ${REVISION_TARGET}
  WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
)

set (CSAN_OUTPUT_DIR Columbus/CSAN.dir/netcoreapp3.1)

set_target_properties (${CSHARP_BUILD_TARGET} PROPERTIES FOLDER ${CMAKE_SUPPORT_FOLDER_NAME})

set (CSAN_TARGET CSAN_virtual_target)

add_custom_target (
    ${CSAN_TARGET}
)

foreach (BINARY ${CSAN_BINARIES})
  add_custom_generated_copy_dependency(${CSAN_TARGET} ${CSAN_OUTPUT_DIR} ${EXECUTABLE_OUTPUT_PATH} ${BINARY} ${CSHARP_BUILD_TARGET})
endforeach()

set_visual_studio_project_folder(${CSAN_TARGET} FALSE)
